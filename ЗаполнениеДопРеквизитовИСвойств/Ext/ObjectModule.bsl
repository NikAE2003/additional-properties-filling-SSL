Процедура ОчиститьЗначенияСвойствОбъекта(Знач ВладелецСвойств, Знач ОчищаемыеСвойства) Экспорт
	
	Если ТипЗнч(ОчищаемыеСвойства) <> Тип("Массив") Тогда
		ОчищаемыеСвойства = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОчищаемыеСвойства);	
	КонецЕсли;

	ТипСвойстваСведение = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ОчищаемыеСвойства, "ЭтоДополнительноеСведение");
	
	ОчищаемыеРеквизиты = Новый Соответствие;
	ОчищаемыеСведения = Новый Соответствие;
	
	Для каждого ОчищаемоеСвойство Из ОчищаемыеСвойства Цикл
		ЭтоДопСведение = ТипСвойстваСведение[ОчищаемоеСвойство];
		Если ЭтоДопСведение Тогда
			ОчищаемыеСведения.Вставить(ОчищаемоеСвойство, Истина);
		Иначе
			ОчищаемыеРеквизиты.Вставить(ОчищаемоеСвойство, Истина);
		КонецЕсли;	
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		Если ОчищаемыеСведения.Количество() <> 0 Тогда
	
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДополнительныеСведения");
			ЭлементБлокировки.УстановитьЗначение("Объект", ВладелецСвойств);
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();	
			НаборЗаписей.Отбор.Объект.Установить(ВладелецСвойств);
			НаборЗаписей.Прочитать();
			
			УдаляемыеЗаписи = Новый Массив;
			Для каждого Запись Из НаборЗаписей Цикл
				Если ОчищаемыеСведения[Запись.Свойство] <> Неопределено Тогда
					УдаляемыеЗаписи.Добавить(Запись);
				КонецЕсли;	
			КонецЦикла;
			
			Для каждого Запись Из УдаляемыеЗаписи Цикл
				НаборЗаписей.Удалить(Запись);
			КонецЦикла;
			
			НаборЗаписей.Записать(Истина);
		
		КонецЕсли;
		
		Если ОчищаемыеРеквизиты.Количество() <> 0 Тогда
		
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ВладелецСвойств.Метаданные().ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ВладелецСвойств);
			Блокировка.Заблокировать();
			
			Объект = ВладелецСвойств.ПолучитьОбъект();
			ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
			
			ДополнительныеРеквизиты = Объект.ДополнительныеРеквизиты;
			
			УдаляемыеСтроки = Новый Массив;
			Для каждого Строка Из ДополнительныеРеквизиты Цикл
				Если ОчищаемыеРеквизиты[Строка.Свойство] <> Неопределено Тогда
					УдаляемыеСтроки.Добавить(Строка);
				КонецЕсли;		
			КонецЦикла;
			
			Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
				ДополнительныеРеквизиты.Удалить(ДополнительныеРеквизиты.Индекс(УдаляемаяСтрока));
			КонецЦикла;
			
			Объект.Записать();
		
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры